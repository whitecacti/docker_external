terraform {
  required_providers {
    coder = {
      source = "coder/coder"
    }
    docker = {
      source = "kreuzwerker/docker"
    }
  }
}

locals {
  username = data.coder_workspace_owner.me.name

  # Load modular startup scripts
  script_init_home     = file("${path.module}/scripts/init_home.sh")
  script_setup_python  = file("${path.module}/scripts/setup_python.sh")
  script_install_tools = file("${path.module}/scripts/install_tools.sh")
  script_startup       = file("${path.module}/scripts/startup.sh")
}

variable "docker_socket" {
  default     = ""
  description = "(Optional) Docker socket URI"
  type        = string
}

provider "docker" {
  # Defaulting to null if the variable is an empty string lets us have an optional variable without having to set our own default
  host = var.docker_socket != "" ? var.docker_socket : null
}

data "coder_provisioner" "me" {}
data "coder_workspace" "me" {}
data "coder_workspace_owner" "me" {}

resource "coder_agent" "main" {
  arch           = data.coder_provisioner.me.arch
  os             = "linux"
  startup_script = <<-EOT
    set -e

    # Create scripts directory
    SCRIPTS_DIR="$HOME/.coder-scripts"
    mkdir -p "$SCRIPTS_DIR"

    # Write modular scripts to disk
    cat > "$SCRIPTS_DIR/init_home.sh" << 'SCRIPT_EOF'
${local.script_init_home}
SCRIPT_EOF

    cat > "$SCRIPTS_DIR/setup_python.sh" << 'SCRIPT_EOF'
${local.script_setup_python}
SCRIPT_EOF

    cat > "$SCRIPTS_DIR/install_tools.sh" << 'SCRIPT_EOF'
${local.script_install_tools}
SCRIPT_EOF

    cat > "$SCRIPTS_DIR/startup.sh" << 'SCRIPT_EOF'
${local.script_startup}
SCRIPT_EOF

    # Make scripts executable
    chmod +x "$SCRIPTS_DIR"/*.sh

    # Run the main startup orchestrator
    "$SCRIPTS_DIR/startup.sh"
  EOT

  # These environment variables allow you to make Git commits right away after creating a
  # workspace. Note that they take precedence over configuration defined in ~/.gitconfig!
  # You can remove this block if you'd prefer to configure Git manually or using
  # dotfiles. (see docs/dotfiles.md)
  env = {
    GIT_AUTHOR_NAME     = coalesce(data.coder_workspace_owner.me.full_name, data.coder_workspace_owner.me.name)
    GIT_AUTHOR_EMAIL    = "${data.coder_workspace_owner.me.email}"
    GIT_COMMITTER_NAME  = coalesce(data.coder_workspace_owner.me.full_name, data.coder_workspace_owner.me.name)
    GIT_COMMITTER_EMAIL = "${data.coder_workspace_owner.me.email}"
  }

  # The following metadata blocks are optional. They are used to display
  # information about your workspace in the dashboard. You can remove them
  # if you don't want to display any information.
  # For basic resources, you can use the `coder stat` command.
  # If you need more control, you can write your own script.
  metadata {
    display_name = "CPU Usage"
    key          = "0_cpu_usage"
    script       = "coder stat cpu"
    interval     = 10
    timeout      = 1
  }

  metadata {
    display_name = "RAM Usage"
    key          = "1_ram_usage"
    script       = "coder stat mem"
    interval     = 10
    timeout      = 1
  }

  metadata {
    display_name = "Home Disk"
    key          = "3_home_disk"
    script       = "coder stat disk --path $${HOME}"
    interval     = 60
    timeout      = 1
  }

  metadata {
    display_name = "CPU Usage (Host)"
    key          = "4_cpu_usage_host"
    script       = "coder stat cpu --host"
    interval     = 10
    timeout      = 1
  }

  metadata {
    display_name = "Memory Usage (Host)"
    key          = "5_mem_usage_host"
    script       = "coder stat mem --host"
    interval     = 10
    timeout      = 1
  }

  metadata {
    display_name = "Load Average (Host)"
    key          = "6_load_host"
    # get load avg scaled by number of cores
    script   = <<EOT
      echo "`cat /proc/loadavg | awk '{ print $1 }'` `nproc`" | awk '{ printf "%0.2f", $1/$2 }'
    EOT
    interval = 60
    timeout  = 1
  }

  metadata {
    display_name = "Swap Usage (Host)"
    key          = "7_swap_host"
    script       = <<EOT
      free -b | awk '/^Swap/ { printf("%.1f/%.1f", $3/1024.0/1024.0/1024.0, $2/1024.0/1024.0/1024.0) }'
    EOT
    interval     = 10
    timeout      = 1
  }

  metadata {
    display_name = "Python Version"
    key          = "8_python_version"
    script       = <<EOT
      if [ -f ~/.python_setup_done ]; then
        export PYENV_ROOT="$HOME/.pyenv"
        export PATH="$PYENV_ROOT/bin:$PATH"
        eval "$(pyenv init -)"
        python --version 2>/dev/null || echo "Python not ready"
      else
        echo "Setting up..."
      fi
    EOT
    interval     = 30
    timeout      = 5
  }
}

# See https://registry.coder.com/modules/coder/code-server
module "code-server" {
  count  = data.coder_workspace.me.start_count
  source = "registry.coder.com/coder/code-server/coder"

  # This ensures that the latest non-breaking version of the module gets downloaded, you can also pin the module version to prevent breaking changes in production.
  version = "~> 1.0"

  agent_id = coder_agent.main.id
  order    = 1

  # Install Python and Jupyter extensions
  extensions = [
    "ms-python.python",
    "ms-python.debugpy",
    # "ms-python.vscode-pylance",
    # "ms-python.vscode-python-envs",
    "ms-vscode.live-server",
    "ms-toolsai.jupyter",
    "ms-toolsai.jupyter-keymap",
    "ms-toolsai.jupyter-renderers",
    "ms-toolsai.vscode-jupyter-cell-tags",
    "ms-toolsai.vscode-jupyter-slideshow",
    "ms-toolsai.vscode-jupyter-powertoys",
    "redhat.vscode-yaml",
    # "Boto3typed.boto3-ide",
    "yy0931.save-as-root",
    "ms-azuretools.vscode-docker"
  ]
}

# # See https://registry.coder.com/modules/coder/jetbrains-gateway
# module "jetbrains_gateway" {
#   count  = data.coder_workspace.me.start_count
#   source = "registry.coder.com/coder/jetbrains-gateway/coder"

#   # JetBrains IDEs to make available for the user to select
#   jetbrains_ides = ["IU", "PS", "WS", "PY", "CL", "GO", "RM", "RD", "RR"]
#   default        = "IU"

#   # Default folder to open when starting a JetBrains IDE
#   folder = "/home/coder"

#   # This ensures that the latest non-breaking version of the module gets downloaded, you can also pin the module version to prevent breaking changes in production.
#   version = "~> 1.0"

#   agent_id   = coder_agent.main.id
#   agent_name = "main"
#   order      = 2
# }

module "filebrowser" {
  count    = data.coder_workspace.me.start_count
  source   = "registry.coder.com/coder/filebrowser/coder"
  version  = "1.1.2"
  agent_id = coder_agent.main.id
  folder   = "/home/coder"
  agent_name = "main"
  subdomain  = false

}

# module "coder-login" {
#   count    = data.coder_workspace.me.start_count
#   source   = "registry.coder.com/coder/coder-login/coder"
#   version  = "1.1.0"
#   agent_id = coder_agent.main.id
# }

# data "coder_parameter" "ai_prompt" {
#   type        = "string"
#   name        = "AI Prompt"
#   default     = ""
#   description = "Write a prompt for Claude Code"
#   mutable     = true
# }

# module "claude-code" {
#   source              = "registry.coder.com/coder/claude-code/coder"
#   version             = "2.1.0"
#   agent_id            = coder_agent.main.id
#   folder              = "/home/coder"
#   install_claude_code = true
#   claude_code_version = "latest"
# }

resource "docker_volume" "home_volume" {
  name = "coder-${data.coder_workspace.me.id}-home"
  # Protect the volume from being deleted due to changes in attributes.
  lifecycle {
    ignore_changes = all
  }
  # Add labels in Docker to keep track of orphan resources.
  labels {
    label = "coder.owner"
    value = data.coder_workspace_owner.me.name
  }
  labels {
    label = "coder.owner_id"
    value = data.coder_workspace_owner.me.id
  }
  labels {
    label = "coder.workspace_id"
    value = data.coder_workspace.me.id
  }
  # This field becomes outdated if the workspace is renamed but can
  # be useful for debugging or cleaning out dangling volumes.
  labels {
    label = "coder.workspace_name_at_creation"
    value = data.coder_workspace.me.name
  }
}

resource "docker_container" "workspace" {
  count = data.coder_workspace.me.start_count
  image = "codercom/enterprise-base:ubuntu"
  # Uses lower() to avoid Docker restriction on container names.
  name = "coder-${data.coder_workspace_owner.me.name}-${lower(data.coder_workspace.me.name)}"
  # Hostname makes the shell more user friendly: coder@my-workspace:~$
  hostname = data.coder_workspace.me.name
  # Use the docker gateway if the access URL is 127.0.0.1
  entrypoint = ["sh", "-c", replace(coder_agent.main.init_script, "/localhost|127\\.0\\.0\\.1/", "host.docker.internal")]
  env        = ["CODER_AGENT_TOKEN=${coder_agent.main.token}"]
  host {
    host = "host.docker.internal"
    ip   = "host-gateway"
  }
  volumes {
    container_path = "/home/coder"
    volume_name    = docker_volume.home_volume.name
    read_only      = false
  }

  # Add labels in Docker to keep track of orphan resources.
  labels {
    label = "coder.owner"
    value = data.coder_workspace_owner.me.name
  }
  labels {
    label = "coder.owner_id"
    value = data.coder_workspace_owner.me.id
  }
  labels {
    label = "coder.workspace_id"
    value = data.coder_workspace.me.id
  }
  labels {
    label = "coder.workspace_name"
    value = data.coder_workspace.me.name
  }
}